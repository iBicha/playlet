import "pkg:/components/Services/SponsorBlock/Notifications/SponsorBlockNotificationUtils.bs"
import "pkg:/components/Services/SponsorBlock/SponsorBlockService.bs"
import "pkg:/source/AsyncTask/AsyncTask.bs"
import "pkg:/source/AsyncTask/Tasks.bs"
import "SponsorBlockSegmentAction.bs"
import "VideoPlayer.bs"

namespace SponsorBlock

    function SetupSponsorBlock() as void
        if not m.preferences["sponsorblock.enabled"]
            return
        end if

        m.sponsorBlockService = new SponsorBlock.SponsorBlockService()
        SponsorBlock.SetVisible(m.notifications, m.container.fullscreen)
    end function

    function FetchSponsorBlock(metadata as object) as void
        if not m.preferences["sponsorblock.enabled"]
            return
        end if

        if m.sponsorBlockTask <> invalid
            m.sponsorBlockTask.cancel = true
        end if

        m.sponsorBlockTask = AsyncTask.Start(Tasks.SponsorBlockTask, {
            metadata: metadata
            showNotifications: m.preferences["sponsorblock.show_notifications"]
            categories: m.preferences["sponsorblock.categories"]
        }, OnSponsorBlockTaskResults)
    end function

    function OnSponsorBlockTaskResults(output as object) as void
        m.sponsorBlockTask = invalid
        if output.cancelled or m.onCloseCalled = true
            return
        end if

        if not output.success
            error = ErrorUtils.Format(output.error)
            LogError(error)
            return
        end if

        skipSegments = output.result.skipSegments
        if skipSegments = invalid or skipSegments.Count() = 0
            return
        end if

        m.skipSegments = skipSegments
        m.top.seekMode = "accurate"

        barPath = output.result.barPath
        if barPath <> invalid
            m.top.trickPlayBar.trackImageUri = barPath
            m.top.trickPlayBar.filledBarBlendColor = "#FF000080"
        end if

        m.top.ObserveFieldScoped("position", FuncName(OnPositionChangeSkipSponsorBlockSections))

        if m.top.hasField("trickPlayPosition")
            PositionChapterLabel()
            m.chapterLabelTimer = m.top.findNode("chapterLabelTimer")
            m.chapterLabelTimer.observeField("fire", FuncName(OnChapterLabelTimer))
            m._sponsorBlockLastTimeChaptedUpdated = 0.0#
            m.top.trickPlayBar.observeField("visible", FuncName(OnTrickPlayBarVisible))
        end if
    end function

    function PositionChapterLabel()
        m.chapterLabel = m.top.findNode("chapterLabel")
        ' We move the chapter label under m.top.trickPlayBar
        ' We could move it to m.top.pivotNode, but that is only available on Roku OS 11.5+
        m.chapterLabel.reparent(m.top.trickPlayBar, false)
        trickPlayBarWidth = m.top.trickPlayBar.boundingRect().width
        m.chapterLabel.translation = [trickPlayBarWidth / 4 - m.chapterLabel.width / 2, 25]
    end function

    function OnTrickPlayBarVisible()
        if m.top.trickPlayBar.visible
            m.top.chapter = ""
            m.chapterLabelTimer.control = "start"
        else
            m.chapterLabelTimer.control = "stop"
        end if
    end function

    function OnChapterLabelTimer() as void
        trickPlayPosition = m.top.trickPlayPosition
        if m._sponsorBlockLastTimeChaptedUpdated = trickPlayPosition
            return
        end if

        m._sponsorBlockLastTimeChaptedUpdated = trickPlayPosition
        UpdateSponsorBlockChapter(trickPlayPosition)
    end function

    function UpdateSponsorBlockChapter(time as double) as void
        segments = m.skipSegments
        segment = FindSegmentByTime(segments, time, 0)
        if segment = invalid
            m.top.chapter = ""
            return
        end if
        m.top.chapter = segment.title
    end function

    function FindSegmentByTime(segments as object, time as dynamic, endOffset as integer) as object
        low = 0
        high = segments.Count() - 1
        while low <= high
            middle = (low + high) \ 2
            segment = segments[middle]
            if time < segment.start
                high = middle - 1
            else if time > (segment.end + endOffset)
                low = middle + 1
            else
                return segment
            end if
        end while
        return invalid
    end function

    function OnPositionChangeSkipSponsorBlockSections() as void
        segments = m.skipSegments
        if segments = invalid or segments.Count() = 0
            return
        end if

        position = m.top.position

        ' We add a small offset to the end position of the segment.
        ' This is so that when we skip a segment, we don't get
        ' another notification to skip it.
        ' Seeking accuracy is not perfect, so we need to account for that.
        segment = FindSegmentByTime(segments, position, -1)
        if segment = invalid
            return
        end if

        action = segment.option

        if segment.showNotification
            SponsorBlock.ShowNotifcation(m.notifications, segment, action)

            if action = SegmentAction.ManualSkip
                ' We'll save the segment here, and handle it in case the user
                ' presses the OK button on the notification.
                m.manualSkipSegment = segment
                return
            end if
        end if

        if action = SegmentAction.AutoSkip
            SkipSegment(segment, position)
        end if
    end function

    function SkipSegment(segment as object, position as dynamic) as void
        segmentEnd = segment.end

        duration = m.top.duration
        ' Sometimes segmentEnd is greater than duration
        if duration > 0 and segmentEnd > duration
            segmentEnd = duration
        end if

        ' If we're skipping a very short distance, it's not worth it
        ' since we can cause rebuffering
        if Abs(segmentEnd - position) < 2
            LogInfo("Skip position is within 2 seconds of segment end, not skipping")
            return
        end if

        ' If the segment is at the end of the video, skip the video instead of seeking
        if duration > 0 and Abs(segmentEnd - duration) < 2
            LogInfo("Skipping section:", segment.category, "at end of video")
            OnVideoFinished()
        else
            LogInfo("Skipping section:", segment.category)
            m.top.seek = segmentEnd
        end if
        SendSkippedSectionEvent(segment.UUID)
    end function

    function ManualSkipSegment() as boolean
        if m.manualSkipSegment = invalid
            return false
        end if

        segment = m.manualSkipSegment
        m.manualSkipSegment = invalid

        notification = SponsorBlock.GetCurrentNotification(m.notifications)
        if notification = invalid
            return false
        end if

        notificationSegmentId = notification.segmentId
        if notificationSegmentId <> segment.UUID
            return false
        end if

        position = m.top.position
        if (position < segment.start) or (segment.end < position)
            return false
        end if

        SkipSegment(segment, position)
        SponsorBlock.RemoveNotifcation(m.notifications)
        return true
    end function

    function SendSkippedSectionEvent(segmentId as string)
        if m.sponsorSkipEventTask <> invalid
            m.sponsorSkipEventTask.cancel = true
        end if

        m.sponsorSkipEventTask = AsyncTask.Start(Tasks.SponsorBlockSegmentViewedTask, { segmentId: segmentId })
    end function

end namespace
