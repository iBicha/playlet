import "pkg:/components/Services/Innertube/Constants.bs"
import "pkg:/source/services/HttpClient.bs"
import "pkg:/source/utils/Logging.bs"
import "pkg:/source/utils/ObjectUtils.bs"
import "pkg:/source/utils/RegistryUtils.bs"
import "pkg:/source/utils/StringUtils.bs"
import "pkg:/source/utils/TimeUtils.bs"

namespace Innertube

    const SESSION_DATA_VERSION = 1
    const INNERTUBE_VISITOR_DATA_LIFESPAN_SECONDS = 60 * 60 * 24 * 14 ' 14 days

    function GetSessionData(cancellation = invalid as object) as object
        sessionData = ReadSessionDataFromRegistry()
        if sessionData <> invalid
            return sessionData
        end if

        sessionData = RequestSessionData(cancellation)
        if sessionData.error <> invalid
            return sessionData
        end if
        SaveSessionDataToRegistry(sessionData)
        return sessionData
    end function

    function RequestSessionData(cancellation = invalid as object) as object
        request = HttpClient.Get("https://www.youtube.com/sw.js_data")
        request.Headers({
            "Accept-Language": "en-US,en;q=0.9"
            "User-Agent": INNERTUBE_WEB_USER_AGENT
            "Accept": "*/*"
            "Referer": "https://www.youtube.com/sw.js"
        })
        request.Cancellation(cancellation)
        request.TryCount(2)

        response = request.Await()
        if not response.IsSuccess()
            return {
                "error": "Failed to fetch session data: " + response.ErrorMessage()
            }
        end if

        text = response.Text()
        text = text.Replace(`)]}'`, "")

        json = StringUtils.ParseJsonSafe(text)
        if json = invalid
            return {
                "error": "Failed to parse session data: " + text
            }
        end if

        visitorData = ObjectUtils.Dig(json, [0, 2, 0, 0, 13])
        if StringUtils.IsNullOrEmpty(visitorData)
            return {
                "error": "Failed to find visitorData in session data: " + text
            }
        end if

        return {
            "timestamp": TimeUtils.Now().AsSeconds()
            "visitorData": visitorData
        }
    end function

    function SaveSessionDataToRegistry(sessionData as object) as void
        sessionData.__version = SESSION_DATA_VERSION

        RegistryUtils.Write(RegistryUtils.INNERTUBE_SESSION_DATA, FormatJson(sessionData))
    end function

    function ReadSessionDataFromRegistry() as object
        sessionData = RegistryUtils.Read(RegistryUtils.INNERTUBE_SESSION_DATA)
        if StringUtils.IsNullOrEmpty(sessionData)
            return invalid
        end if

        sessionData = StringUtils.ParseJsonSafe(sessionData)
        if sessionData = invalid
            RegistryUtils.Delete(RegistryUtils.INNERTUBE_SESSION_DATA)
            return invalid
        end if

        timestamp = sessionData.timestamp
        nowSeconds = TimeUtils.Now().AsSeconds()
        if nowSeconds - timestamp > INNERTUBE_VISITOR_DATA_LIFESPAN_SECONDS
            RegistryUtils.Delete(RegistryUtils.INNERTUBE_SESSION_DATA)
            return invalid
        end if

        return sessionData
    end function
end namespace
