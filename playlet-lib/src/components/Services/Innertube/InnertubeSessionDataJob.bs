import "pkg:/components/Services/Innertube/SessionData.bs"

@job("InnertubeSessionDataJob")
function ExecuteJob() as void
    input = JobGetInput()

    innertubeNode = input.innertube

    cancellation = CancellationUtils.CreateCancellation(JobGetCallbackNode(), "cancel", true)
    sessionData = Innertube.GetSessionData(cancellation)
    if sessionData = invalid
        JobError("Failed to fetch session data")
        return
    end if

    innertubeNode.queueFields(true)
    if IsInt(sessionData.timestamp)
        innertubeNode.timestamp = sessionData.timestamp
    end if

    if not StringUtils.IsNullOrEmpty(sessionData.visitorData)
        innertubeNode.visitorData = sessionData.visitorData
    end if

    innertubeNode.queueFields(false)
end function
