namespace SponsorBlock
    const NOTIFICATION_NODE_ID = "SponsorBlockNotification"

    function GetCurrentNotification(notifications as object) as object
        childCount = notifications.getChildCount()

        for i = 0 to childCount - 1
            child = notifications.getChild(i)
            if child.id = NOTIFICATION_NODE_ID
                return child
            end if
        end for

        return invalid
    end function

    function ShowNotifcation(notifications as object, segment as object) as void
        ' TODO:P2 avoid finding the node over and over
        ' The video player can probably own a single notification, and destroy it when the video is closed
        notification = GetCurrentNotification(notifications)
        if notification = invalid
            notification = notifications.createChild("SponsorBlockNotification")
            notification.id = NOTIFICATION_NODE_ID
        end if

        notification.segmentId = segment.UUID
        notification.categoryText = segment.shortTitle
        notification.actionText = segment.actionText
        notification.doShow = true
    end function

    function RemoveNotifcation(notifications as object) as void
        notification = GetCurrentNotification(notifications)
        if notification <> invalid
            notifications.RemoveChild(notification)
        end if
    end function

    function SetVisible(notifications as object, visible as boolean)
        notification = GetCurrentNotification(notifications)
        if notification <> invalid
            notification.visible = visible
        end if
    end function

end namespace
