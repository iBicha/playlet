import "pkg:/source/utils/Logging.bs"
import "pkg:/source/utils/StringUtils.bs"

function Init()
    m.top.functionName = "JobTaskLoop"
end function

function JobTaskLoop() as void
    msgPort = CreateObject("roMessagePort")

    m.top.observeField("shutdown", msgPort)
    m.top.observeField("request", msgPort)

    m.top.ready = true

    while true
        msg = wait(0, msgPort)
        msgType = type(msg)

        if msgType = "roSGNodeEvent"
            field = msg.GetField()
            if field = "shutdown" and msg.getData()
                m.top.request = invalid
                return
            end if
            if field = "request"
                request = msg.getData()
                ExecuteJobRequest(request)
                m.top.ready = true
            end if
        end if
    end while
end function

function ExecuteJobRequest(request as object) as void
    job = CreateObject("roSGNode", request.jobName)
    job.id = request.jobName + "-" + StringUtils.RandomId()
    if job = invalid
        LogError("Failed to create job node:", request.jobName)
        return
    end if

    if request.input <> invalid
        job.input = request.input
    end if

    LogDebug("Executing job:", request.jobName)
    job@.Execute()
end function
