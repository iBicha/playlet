import "pkg:/source/utils/Logging.bs"
import "pkg:/source/utils/StringUtils.bs"
import "pkg:/source/utils/Types.bs"

function Init()
    m.top.functionName = "LoggerLoop"

    port = CreateObject("roMessagePort")

    m.top.ObserveField("logLine", port)

    scene = m.top.getScene()
    scene.ObserveFieldScoped("systemLogEvent", port)

    m.port = port

    #if DEBUG
        StartLogger(m.top, LogLevel.Debug)
    #else
        StartLogger(m.top, LogLevel.Info)
    #end if
end function

function StartLogger(logger as object, logLevel = LogLevel.Info as LogLevel) as void
    if m.global.logger <> invalid
        return
    end if

    logger.logLevel = logLevel
    m.global.addFields({ logger: logger })
    logger.control = "run"
end function

function LoggerLoop()
    port = m.port

    buffer = CreateObject("roByteArray")
    logsFile = m.top.folder + "/" + m.top.filename
    previousLogsFile = m.top.folder + "/" + m.top.filenamePrevious
    filenameStartup = m.top.filenameStartup
    dateTime = CreateObject("roDateTime")

    CreateDirectory(m.top.folder)
    DeleteFile(previousLogsFile)
    MoveFile(logsFile, previousLogsFile)

    startUpLogs = ReadAsciiFile(filenameStartup)
    if not StringUtils.IsNullOrEmpty(startUpLogs)
        line = `************* Startup Logs *************\n${startUpLogs}*********** End Startup Logs ***********\n`
        ' bs:disable-next-line LINT3012
        print line
        buffer.FromAsciiString(line)
        buffer.AppendFile(logsFile)
    end if

    appMemoryMonitor = CreateObject("roAppMemoryMonitor")
    if appMemoryMonitor <> invalid and FindMemberFunction(appMemoryMonitor, "EnableMemoryWarningEvent") <> invalid
        appMemoryMonitor.SetMessagePort(port)
        appMemoryMonitor.EnableMemoryWarningEvent(true)
    end if

    deviceInfo = CreateObject("roDeviceInfo")
    deviceInfo.SetMessagePort(port)
    deviceInfo.EnableLowGeneralMemoryEvent(true)
    m.lastDeviceInfoMemoryLevel = ""

    while true
        msg = wait(0, port)
        msgType = type(msg)
        if msgType = "roSGNodeEvent"
            field = msg.getField()
            if field = "logLine"
                OnLineLog(msg, dateTime, logsFile, buffer)
            else if field = "systemLogEvent"
                OnLineSysLog(msg, dateTime, logsFile, buffer)
            end if
        else if msgType = "roAppMemoryNotificationEvent"
            OnLineLogMonitorMemoryWarning(msg, dateTime, logsFile, buffer)
        else if msgType = "roDeviceInfoEvent"
            OnLineLogDeviceInfoMemoryWarning(msg, dateTime, logsFile, buffer)
        end if
    end while
end function

function OnLineLog(event as object, dateTime as object, logsFile as string, buffer as object)
    line = FormatTime(dateTime) + event.getData()
    ' bs:disable-next-line LINT3012
    print line

    buffer.FromAsciiString(line + `\n`)
    buffer.AppendFile(logsFile)
end function

function OnLineSysLog(event as object, dateTime as object, logsFile as string, buffer as object) as void
    info = event.getData()
    if info.LogType <> "http.error"
        return
    end if
    line = FormatTime(dateTime) + "[ERROR][SysLog] " + ToString(info)
    ' bs:disable-next-line LINT3012
    print line

    buffer.FromAsciiString(line + `\n`)
    buffer.AppendFile(logsFile)
end function

function OnLineLogMonitorMemoryWarning(event as object, dateTime as object, logsFile as string, buffer as object) as void
    info = event.getInfo()
    line = FormatTime(dateTime) + "[WARN][AppMonitorMemory] " + ToString(info)

    ' bs:disable-next-line LINT3012
    print line

    buffer.FromAsciiString(line + `\n`)
    buffer.AppendFile(logsFile)
end function

function OnLineLogDeviceInfoMemoryWarning(event as object, dateTime as object, logsFile as string, buffer as object) as void
    info = event.getInfo()
    if not IsString(info.generalMemoryLevel)
        return
    end if

    ' roDeviceInfoEvent can be a bit spammy, so only log if the memory level has changed
    if info.generalMemoryLevel = m.lastDeviceInfoMemoryLevel
        return
    end if
    m.lastDeviceInfoMemoryLevel = info.generalMemoryLevel

    line = FormatTime(dateTime) + "[WARN][DeviceInfoMemory] " + ToString(info)

    ' bs:disable-next-line LINT3012
    print line

    buffer.FromAsciiString(line + `\n`)
    buffer.AppendFile(logsFile)
end function

' Note: although the timestamp should be captured in the caller, not the logger,
' it is done here to avoid adding overhead to the caller thread.
function FormatTime(dateTime as object) as string
    dateTime.Mark()
    dateTime.ToLocalTime()

    hours = dateTime.GetHours().ToStr()
    if hours.Len() = 1
        hours = "0" + hours
    end if

    minutes = dateTime.GetMinutes().ToStr()
    if minutes.Len() = 1
        minutes = "0" + minutes
    end if

    seconds = dateTime.GetSeconds().ToStr()
    if seconds.Len() = 1
        seconds = "0" + seconds
    end if

    milliseconds = dateTime.GetMilliseconds().ToStr()
    if milliseconds.Len() = 1
        milliseconds = "00" + milliseconds
    else if milliseconds.Len() = 2
        milliseconds = "0" + milliseconds
    end if

    return "[" + hours + ":" + minutes + ":" + seconds + "." + milliseconds + "]"
end function
