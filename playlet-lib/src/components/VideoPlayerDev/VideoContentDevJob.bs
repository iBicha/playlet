import "pkg:/components/Services/Innertube/InnertubeService.bs"
import "pkg:/components/Services/Invidious/InvidiousService.bs"
import "pkg:/components/Services/Invidious/InvidiousToContentNode.bs"

@job("VideoContentDevJob")
function ExecuteJob() as void
    input = JobGetInput()

    titleLabel = input.titleLabel
    secondaryTitleLabel = input.secondaryTitleLabel
    viewCountLabel = input.viewCountLabel
    backgroundPoster = input.backgroundPoster
    contentNode = input.content
    invidiousNode = input.invidious
    preferencesNode = input.preferences

    titleLabel.text = contentNode.title
    secondaryTitleLabel.text = contentNode.secondaryTitle

    videoId = contentNode.videoId

    if StringUtils.IsNullOrEmpty(videoId)
        JobError("Can't load video information: videoId is missing")
        return
    end if

    service = new Invidious.InvidiousService(invidiousNode)
    instance = service.GetInstance()

    backgroundPoster.uri = `${instance}/vi/${videoId}/maxresdefault.jpg`

    cancellation = JobGetCancellation()
    backend = preferencesNode["backend.selected"]
    if backend = "playlet"
        metadata = InnertubeService.GetVideoMetadata(videoId, { cancellation: cancellation })
    else
        metadata = service.GetVideoMetadata(videoId, { cancellation: cancellation })
    end if

    if JobIsCancelled()
        JobCancel()
        return
    end if

    if metadata.error <> invalid
        JobError(metadata.error)
        return
    end if

    contentNode.title = metadata.title
    titleLabel.text = metadata.title

    ' Set author as secondary title
    secondaryTitle = `@${metadata.author}`
    contentNode.secondaryTitle = secondaryTitle
    secondaryTitleLabel.text = secondaryTitle

    ' Set view count and publish date in separate label
    viewCountText = ""
    viewCount = InvidiousContent.VideoGetViewCountText(metadata)
    if not StringUtils.IsNullOrEmpty(viewCount)
        viewCountText = viewCount
    end if
    publishedText = InvidiousContent.VideoGetPublishedText(metadata)
    if not StringUtils.IsNullOrEmpty(publishedText)
        if viewCountText.Len() > 0
            viewCountText = `${viewCountText} â€¢ ${publishedText}`
        else
            viewCountText = publishedText
        end if
    end if
    if viewCountLabel <> invalid
        viewCountLabel.text = viewCountText
    end if

    contentNode.live = metadata.liveNow

    SetCaptions(metadata, instance, contentNode)

    streamUrls = CreateStreamUrls(metadata)
    contentNode.url = streamUrls[0]

    contentNode.addFields({
        metadata: metadata
        ' StreamUrls is taken
        playletStreamUrls: streamUrls
        playletStreamUrlIndex: 0
    })
end function

function SetCaptions(metadata as object, instance as string, contentNode as object) as void
    captions = metadata.captions
    if captions = invalid or captions.Count() = 0
        return
    end if

    isoLanguageList = StringUtils.ParseJsonSafe(ReadAsciiFile("libpkg:/config/ISO-639-1-to-ISO-639-2T.json5"))
    if isoLanguageList = invalid
        return
    end if

    subtitleTracks = []

    for each caption in captions
        code = caption.language_code
        if StringUtils.IsNullOrEmpty(code)
            continue for
        end if

        if code.InStr("-") <> -1
            code = code.Split("-")[0]
        end if

        if code.len() = 2
            if not isoLanguageList.DoesExist(code)
                continue for
            end if
            code = isoLanguageList[code]
        end if

        url = caption.url
        if StringUtils.IsNullOrEmpty(url)
            continue for
        end if

        if not url.StartsWith("http")
            if StringUtils.IsNullOrEmpty(instance)
                continue for
            end if
            url = instance + url
        end if

        label = caption.label
        if StringUtils.IsNullOrEmpty(label)
            label = code
        end if

        subtitleTracks.Push({
            Description: label
            Language: code
            TrackName: url
        })
    end for

    if subtitleTracks.Count() = 0
        return
    end if

    contentNode.ClosedCaptions = True
    contentNode.SubtitleTracks = subtitleTracks
end function

function CreateStreamUrls(metadata as object) as object
    streamUrls = []

    if metadata.hlsUrl <> invalid
        streamUrls.push(metadata.hlsUrl)
    else
        streamUrls.push(`http://127.0.0.1:8888/api/dash?v=${metadata.videoId}`)
    end if

    return streamUrls
end function
