import "pkg:/components/Services/Dash/DashManifest.bs"
import "pkg:/components/Services/Invidious/InvidiousService.bs"
import "pkg:/components/Web/WebServer/Http/HttpRouter.bs"
import "pkg:/source/utils/StringUtils.bs"
import "pkg:/source/utils/Types.bs"

namespace Http

    class DashRouter extends HttpRouter
        function new(server as object)
            super()

            m.videoQueueNode = server.sceneNodes.videoQueue
            m.invidiousNode = server.sceneNodes.invidious
            m.invidiousService = new Invidious.InvidiousService(m.invidiousNode)
            ' No need to pass the invidious instance here, we will refresh it for
            ' each request to make sure we are using the latest one
            m.dashManifest = new DashManifest("")
        end function

        @get("/api/dash")
        function GenerateDashManifest(context as object) as boolean
            request = context.request
            response = context.response

            url = request.query.url
            if not StringUtils.IsNullOrEmpty(url)
                return m.GenerateDashManifestAppendStoryboards(context)
            end if

            v = request.query.v
            local = request.query.local
            quality = request.query.quality

            if StringUtils.IsNullOrEmpty(v)
                response.Default(400, "Missing video id")
                WriteAsciiFile("cachefs:/last_generated_dash.xml", "400 Missing video id")
                return true
            end if

            metadata = ObjectUtils.DigUnsafe(m.videoQueueNode, ["player", "content", "metadata"])
            if not IsAssociativeArray(metadata)
                response.Default(500, "videoQueue.player.content.metadata is invalid")
                WriteAsciiFile("cachefs:/last_generated_dash.xml", "500 videoQueue.player.content.metadata is invalid")
                return true
            end if
            if metadata.videoId <> v
                response.Default(400, "videoQueue.player.content.metadata.videoId does not match the requested video id")
                WriteAsciiFile("cachefs:/last_generated_dash.xml", "400 videoQueue.player.content.metadata.videoId does not match the requested video id")
                return true
            end if

            m.dashManifest.invidiousInstance = m.invidiousService.GetInstance()

            qualityFilter = m.CreateQualityFilter(quality)
            xmlContent = m.dashManifest.FromVideoMetadata(metadata, qualityFilter, ValidString(local) = "true")

            if xmlContent.InStr("[ERROR]") <> -1
                LogErrorNoTelemetry("Dash manifest generated with errors")
                lastPlayerResponse = ValidString(ReadAsciiFile("cachefs:/last_player_response.json"))
                LogTelemetryEvent({
                    level: "error"
                    message: "Dash manifest generated with errors"
                    attachments: [{
                        filename: "dash.xml"
                        content_type: "text/plain"
                        data: xmlContent
                    }, {
                        filename: "last_player_response.json"
                        content_type: "application/json"
                        data: lastPlayerResponse
                    }]
                })
            end if

            response.http_code = 200
            response.SetBodyDataString(xmlContent)
            response.ContentType("application/dash+xml")

            WriteAsciiFile("cachefs:/last_generated_dash.xml", xmlContent)

            return true
        end function

        function CreateQualityFilter(quality as dynamic) as object
            qualityFilter = {}
            if not IsString(quality)
                return qualityFilter
            end if

            includeList = quality.split(",")
            for each quality in includeList
                if quality = "auto"
                    continue for
                end if
                pIndex = quality.InStr("p")
                if pIndex <> -1
                    quality = quality.Mid(0, pIndex)
                end if
                qualityFilter[quality] = true
            end for

            return qualityFilter
        end function

        function GenerateDashManifestAppendStoryboards(context as object) as boolean
            request = context.request
            response = context.response

            url = request.query.url

            storyboards = ObjectUtils.DigUnsafe(m.videoQueueNode, ["player", "content", "metadata", "storyboards"])
            if not IsArray(storyboards) or storyboards.count() = 0
                response.Redirect(url)
                return true
            end if

            dashRequest = HttpClient.Get(url)
            dashResponse = dashRequest.Await()

            if not dashResponse.IsSuccess()
                LogError("Failed to fetch DASH manifest from URL", dashResponse.ErrorMessage())
                response.Redirect(url)
                return true
            end if

            dashContent = ValidString(dashResponse.Text())
            storyboardIndex = dashContent.InStr("</Period>")
            if storyboardIndex = -1
                response.Redirect(url)
                return true
            end if

            xml = new XmlObject()
            manifestObj = new DashManifest("")
            manifestObj.GenerateImageAdaptationSets(xml, storyboards, 100)
            storyboardXml = xml.ToXmlString(false)

            newDashContent = dashContent.Left(storyboardIndex) + storyboardXml + dashContent.Mid(storyboardIndex)
            response.http_code = 200
            response.SetBodyDataString(newDashContent)
            response.ContentType("application/dash+xml")
            return true
        end function

    end class

end namespace
