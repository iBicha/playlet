namespace Http

    class ProfilesRouter extends HttpRouter

        private profilesNode as roSGNodeProfilesService
        private profilesContent as roSGNodeContentNode

        function new(server as HttpServer)
            super()

            ' bs:disable-next-line LINT1001
            m.profilesNode = server.task.profilesService as roSGNodeProfilesService
            m.profilesContent = m.profilesNode.content.getChild(0)
        end function

        @get("/api/profiles")
        function GetProfiles(context as object) as boolean
            response = context.response

            profiles = m.profilesNode@.GetProfilesDto(false)
            response.Json(profiles)
            return true
        end function

        @post("/api/profiles/activate")
        function ActivateProfile(context as object) as boolean
            request = context.request
            response = context.response

            payload = request.Json()
            if payload = invalid
                response.Default(400, `Invalid JSON payload`)
                return true
            end if

            id = payload.id
            if StringUtils.IsNullOrEmpty(id)
                response.Default(400, "Missing profile id")
                return true
            end if

            profile = m.profilesContent.findNode(id)
            if profile = invalid
                response.Default(404, `Profile id "${id}" not found`)
                return true
            end if

            m.profilesNode@.SetCurrentProfile(profile)
            response.Default(200, "Profile updated")
            return true
        end function

        @delete("/api/profiles")
        function Logout(context as object) as boolean
            request = context.request
            response = context.response

            id = request.query.id
            if StringUtils.IsNullOrEmpty(id)
                response.Default(400, "Missing profile id")
                return true
            end if

            profile = m.profilesContent.findNode(id)
            if profile = invalid
                response.Default(404, `Profile id "${id}" not found`)
                return true
            end if

            m.profilesNode@.LogoutWithProfile(profile)
            response.Default(200, "Profile logged out")
            return true
        end function

    end class

end namespace
