import "pkg:/components/Web/WebServer/Http/HttpRouter.bs"

namespace Http

    class CorsMiddleware extends HttpRouter
        private origin
        private headers
        private methods

        function new(origin = "*" as string, headers = "*" as string, methods = "POST, PUT, GET, DELETE, OPTIONS" as string)
            super()

            m.origin = origin
            m.headers = headers
            m.methods = methods
        end function

        @all("*")
        function AddAccessControlHeaders(context as object) as boolean
            request = context.request
            response = context.response

            if not StringUtils.IsNullOrEmpty(m.origin)
                response.headers["Access-Control-Allow-Origin"] = m.origin
            end if

            if not StringUtils.IsNullOrEmpty(m.headers)
                response.headers["Access-Control-Allow-Headers"] = m.headers
            end if

            if not StringUtils.IsNullOrEmpty(m.methods)
                response.headers["Access-Control-Allow-Methods"] = m.methods
            end if

            if request.method = "OPTIONS"
                response.header_only = true
                response.http_code = 200
                return true
            end if
            return false
        end function
    end class

end namespace
