import "pkg:/source/utils/StringUtils.bs"
import "pkg:/source/utils/TimeUtils.bs"
import "pkg:/source/utils/Types.bs"

function Init()
    m.durationRect = m.top.FindNode("durationRect")
    m.durationLabel = m.top.FindNode("durationLabel")
end function

function OnContentSet() as void
    content = m.top.itemContent

    if content = invalid
        if m.progressNode <> invalid
            m.progressNode.unobserveFieldScoped("timestamp")
            m.progressNode = invalid
        end if
        return
    end if

    m.top.title = content.title
    m.top.author = content.author

    if content.isUpcoming = true
        m.top.viewCountDate = content.premiereTimestampText
    else if not StringUtils.IsNullOrEmpty(content.publishedText)
        label = content.publishedText
        if not StringUtils.IsNullOrEmpty(content.viewCountText)
            label = `${label} â€¢ ${content.viewCountText}`
        end if
        m.top.viewCountDate = label
    else
        m.top.viewCountDate = ""
    end if

    m.top.thumbnailUri = content.thumbnail

    lengthSeconds = ValidInt(content.lengthSeconds)
    hasLength = lengthSeconds <> 0
    isUpcoming = not hasLength and content.isUpcoming = true
    isLive = not hasLength and not isUpcoming and content.liveNow = true

    m.top.durationRectVisible = hasLength
    m.top.upcomingRectVisible = isUpcoming
    m.top.liveRectVisible = isLive

    if hasLength
        SetDurationText(content.lengthText)
    end if

    SetupProgress(content)
end function

function SetDurationText(text as string) as void
    if m.top.duration = text
        return
    end if

    m.top.duration = text

    label = m.durationLabel
    rect = m.durationRect

    if label = invalid or rect = invalid
        return
    end if

    rectParent = rect.getParent()

    size = label.localBoundingRect()

    rect.width = size.width + 16
    rect.translation = [rectParent.width - rect.width, rect.translation[1]]
end function

function SetupProgress(content as object)
    if m.progressNode <> invalid
        m.progressNode.unobserveFieldScoped("timestamp")
        m.progressNode = invalid
    end if

    m.progressNode = content.progressNode
    if m.progressNode <> invalid
        m.progressNode.observeFieldScoped("timestamp", FuncName(SetProgress))
    end if
    SetProgress()
end function

function SetProgress() as void
    if m.progressNode = invalid
        m.top.progressRectScale = [0, 1]
        return
    end if

    duration = m.progressNode.duration
    timestamp = m.progressNode.timestamp
    progress = 0
    if duration > 0
        progress = timestamp / duration
    end if
    m.top.progressRectScale = [progress, 1]
end function
