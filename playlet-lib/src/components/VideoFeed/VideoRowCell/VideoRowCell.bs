import "pkg:/components/Fonts/Constants.bs"
import "pkg:/source/utils/Locale.bs"
import "pkg:/source/utils/MathUtils.bs"
import "pkg:/source/utils/StringUtils.bs"
import "pkg:/source/utils/TimeUtils.bs"
import "pkg:/source/utils/Types.bs"

function Init()
    m.titleLabel = m.top.FindNode("titleLabel")
    m.authorLabel = m.top.FindNode("authorLabel")
    m.viewCountDateLabel = m.top.FindNode("viewCountDateLabel")
    m.durationRect = m.top.FindNode("durationRect")
    m.durationLabel = m.top.FindNode("durationLabel")
    m.progressRect = m.top.FindNode("progressRect")

    m.titleLabel.drawingStyles = {
        "Arabic": {
            "fontSize": 18
            "fontUri": ARABIC_FONT_TMP_PATH
        }
        "default": {
            "fontUri": "font:SmallestBoldSystemFont"
        }
    }
    m.authorLabel.drawingStyles = {
        "Arabic": {
            "fontSize": 18
            "fontUri": ARABIC_FONT_TMP_PATH
        }
        "default": {
            "fontUri": "font:SmallestSystemFont"
        }
    }
    m.viewCountDateLabel.drawingStyles = {
        "default": {
            "fontUri": "font:SmallestSystemFont"
        }
    }

    m.CellBadgeFont = m.global.CellBadgeFont
    m.CellBadgeBoldFont = m.global.CellBadgeBoldFont

    m.top.ObserveField("itemContent", FuncName(OnContentSet))
    m.top.ObserveField("percentDurationWatched", FuncName(OnPercentDurationWatched))
end function

function OnContentSet() as void
    content = m.top.itemContent

    if content = invalid
        return
    end if

    m.top.title = content.title
    lines = content.lines

    ' NOTE: "_author" not "author". See PlaylistContentNode.xml for explanation.
    author = content._author
    if StringUtils.IsNullOrEmpty(author)
        if IsArray(lines) and lines.Count() > 0
            author = lines[0]
        end if
    end if
    m.top.author = author

    viewCountDate = ""
    if content.isUpcoming = true
        viewCountDate = content.premiereTimestampText
    else
        publishedText = content.publishedText
        viewCountText = content.viewCountText

        label = publishedText
        if publishedText.Len() > 0 and viewCountText.Len() > 0
            label += " â€¢ "
        end if
        label += viewCountText

        viewCountDate = label
    end if
    if StringUtils.IsNullOrEmpty(viewCountDate)
        if IsArray(lines) and lines.Count() > 1
            viewCountDate = lines[1]
        end if
    end if
    m.top.viewCountDate = viewCountDate

    m.top.thumbnailUri = content.thumbnail

    m.top.percentDurationWatched = content.percentDurationWatched

    if not StringUtils.IsNullOrEmpty(content.lengthText)
        SetDurationLabel(content.lengthText, "#161616FF", 0.8, m.CellBadgeFont)
        m.top.durationRectVisible = true
    else
        if content.isUpcoming = true
            SetDurationLabel(Tr(Locale.RowCell.Upcoming), "#161616FF", 0.8, m.CellBadgeBoldFont)
            m.top.durationRectVisible = true
        else if content.liveNow
            SetDurationLabel(Tr(Locale.RowCell.Live), "#CC0000FF", 0.9, m.CellBadgeBoldFont)
            m.top.durationRectVisible = true
        else
            m.top.durationRectVisible = false
        end if
    end if
end function

function SetDurationLabel(text as string, bgColor as string, bgOpacity as float, font as object) as void
    m.top.duration = text

    rectParent = m.durationRect.getParent()
    m.durationLabel.font = font

    size = m.durationLabel.localBoundingRect()

    m.durationRect.width = size.width + 16
    m.durationRect.translation = [rectParent.width - m.durationRect.width, m.durationRect.translation[1]]
    m.durationRect.color = bgColor
    m.durationRect.opacity = bgOpacity
end function

function OnPercentDurationWatched() as void
    percent = MathUtils.Clamp(m.top.percentDurationWatched, 0, 100) / 100.0
    m.progressRect.scale = [percent, 1]
end function
