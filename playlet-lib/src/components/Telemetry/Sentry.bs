import "pkg:/source/services/HttpClient.bs"

const SENTRY_ENVELOPE_URL = "https://playlet-v8556.ondigitalocean.app/envelope"

const SUPPORTS_ATTACHMENTS = false

namespace Sentry

    function SendEvents(events as object) as integer
        timestamp = CreateObject("roDateTime").ToISOString()

        payload = FormatJson({
            sent_at: timestamp
            sdk: {
                name: "sentry.roku.playlet"
                version: "1.0.0"
            }
        }) + `\n`

        for each event in events
            payload += `{"type":"event"}` + `\n`

            if StringUtils.IsNullOrEmpty(event.event_id)
                event.event_id = m.deviceInfo.GetRandomUUID().Replace("-", "")
            end if

            if StringUtils.IsNullOrEmpty(event.timestamp)
                event.timestamp = timestamp
            end if

            eventPayload = {
                event_id: event.event_id
                timestamp: event.timestamp
                platform: "roku"
                #if DEBUG
                    environment: "development"
                #else
                    environment: "production"
                #end if
                release: event.release
                tags: event.tags
                level: event.level
                user: {
                    id: event.userId
                }
            }

            message = event.message

            if event.fingerprint = invalid
                event.fingerprint = GetFingerprint(message)
            end if

            if event.fingerprint <> invalid
                fingerprint = event.fingerprint
                if not IsArray(fingerprint)
                    fingerprint = [fingerprint]
                end if
                for i = 0 to fingerprint.Count() - 1
                    if not IsString(fingerprint[i])
                        fingerprint[i] = ToString(fingerprint[i])
                    end if
                end for
                eventPayload.fingerprint = fingerprint
            end if

            hasAttachments = IsArray(event.attachments) and event.attachments.Count() > 0

            if (not SUPPORTS_ATTACHMENTS) and hasAttachments
                message += `\nAttachments: (${event.attachments.Count()} files)`
                for each attachment in event.attachments
                    message += `\nAttachment: ${attachment.filename} (${attachment.content_type})\n${attachment.data}`
                end for
                message += `\nEnd of attachments.`
            end if

            ' TODO:P1 compress if message is larger than 8192
            eventPayload.logentry = {
                formatted: message
            }

            payload += FormatJson(eventPayload) + `\n`

            if SUPPORTS_ATTACHMENTS and hasAttachments
                tmpBuffer = CreateObject("roByteArray")
                for each attachment in event.attachments
                    data = attachment.data
                    attachment.Delete("data")
                    attachment["type"] = "attachment"
                    tmpBuffer.FromAsciiString(data)
                    attachment["length"] = tmpBuffer.Count()

                    payload += FormatJson(attachment) + `\n`
                    payload += data + `\n`
                end for
            end if
        end for

        request = HttpClient.Post(SENTRY_ENVELOPE_URL, payload)
        request.Header("Content-Type", "application/x-sentry-envelope")
        response = request.Await()
        if not response.IsSuccess()
            #if DEBUG
                LogErrorNoTelemetry("Failed to send event to Sentry:", response.ErrorMessage(), `\nHeaders:`, response.Headers())
            #end if

            retryAfter = ValidString(response.Headers()["Retry-After"]).ToInt()
            LogErrorNoTelemetry("Rate limited by Sentry. Retry after", retryAfter, "seconds")
            if retryAfter = 0
                retryAfter = 60
            end if
            return retryAfter
        end if

        LogDebug("Sent events to Sentry. Response:", response.Text())
        return 0
    end function

    function GetFingerprint(message as dynamic) as dynamic
        if StringUtils.IsNullOrEmpty(message)
            return invalid
        end if
        ' Replaces `\nRequestId: 12345678\n` with `\nRequestId: <REQUEST_ID>\n`
        fingerprint = ReplaceRequestId(message)
        if fingerprint = invalid
            return invalid
        end if
        return [fingerprint]
    end function

    function ReplaceRequestId(message as string) as dynamic
        reqIdStart = `\nRequestId: `
        reqIdPlaceholder = "<REQUEST_ID>"
        result = message
        replaced = false
        searchIndex = 0
        while true
            requestIdIndex = result.InStr(searchIndex, reqIdStart)
            if requestIdIndex = -1
                exit while
            end if

            reqIdStartIndex = requestIdIndex + reqIdStart.Len()
            newLineIndex = result.InStr(reqIdStartIndex, `\n`)
            if newLineIndex = -1
                exit while
            end if

            reqIdEndIndex = newLineIndex - 1
            if (reqIdEndIndex <= reqIdStartIndex) or ((reqIdEndIndex - reqIdStartIndex) > 20)
                exit while
            end if

            result = result.Mid(0, reqIdStartIndex) + reqIdPlaceholder + result.Mid(reqIdEndIndex + 1)
            replaced = true
            searchIndex = reqIdStartIndex + reqIdPlaceholder.Len()
        end while

        if not replaced
            return invalid
        end if

        return result
    end function
end namespace
