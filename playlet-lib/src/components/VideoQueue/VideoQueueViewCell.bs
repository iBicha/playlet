import "pkg:/source/utils/Logging.bs"
import "pkg:/source/utils/TimeUtils.bs"
import "pkg:/source/utils/Types.bs"

function Init()
    m.titleLabel = m.top.FindNode("titleLabel")
    m.durationLabel = m.top.FindNode("durationLabel")
    m.durationRect = m.top.FindNode("durationRect")

    m.titleLabel.font = m.global.CellTitle1Font
    m.durationLabel.font = m.global.CellBadgeFont

    m.top.ObserveField("itemContent", FuncName(OnContentSet))
    m.top.ObserveField("index", FuncName(OnIndexSet))
end function

function OnContentSet() as void
    content = m.top.itemContent
    if content = invalid
        return
    end if

    m.top.title = content.title
    m.top.thumbnailUri = content.thumbnail

    SetDurationText(content.lengthText)
    SetHighlight()
end function

function OnIndexSet() as void
    SetHighlight()
end function

function SetDurationText(text as string) as void
    hasText = text <> ""
    m.top.durationRectVisible = hasText
    if not hasText
        return
    end if

    m.top.duration = text

    rectParent = m.durationRect.getParent()
    size = m.durationLabel.localBoundingRect()

    m.durationRect.width = size.width + 16
    m.durationRect.translation = [rectParent.width - m.durationRect.width, m.durationRect.translation[1]]
end function

function SetHighlight() as void
    content = m.top.itemContent
    if content = invalid
        return
    end if

    contentIndex = content.queueIndex
    index = m.top.index

    highlighted = contentIndex = index

    if highlighted
        ' bs:disable-next-line LINT3023
        m.top.thumbnailBlendColor = "#FFFFFFFF"
    else
        m.top.thumbnailBlendColor = "#606060FF"
    end if

    m.top.highlightVisible = highlighted
end function
