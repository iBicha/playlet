import "pkg:/components/JobSystem/Jobs.bs"
import "pkg:/components/JobSystem/JobSystem.bs"
import "pkg:/source/utils/Logging.bs"
import "pkg:/source/utils/StringUtils.bs"
import "pkg:/source/utils/TimeUtils.bs"

function HasValidAccessToken(_unused as dynamic) as boolean
    serverType = m.top.type
    accessToken = m.top.accessToken
    if StringUtils.IsNullOrEmpty(accessToken)
        return false
    end if

    if serverType = "invidious"
        return true
    end if

    nowSeconds = TimeUtils.Now().AsSeconds()
    expiresTimestamp = m.top.expiresTimestamp

    ' We should refresh the token if it's about to expire in 30 minutes
    ' Although the current token might be valid, we can let current request
    ' wait for the new token
    shouldRefreshToken = expiresTimestamp < nowSeconds + 60 * 30

    if shouldRefreshToken
        RefreshAccessToken()
    end if

    return not shouldRefreshToken
end function

function RefreshAccessToken() as void
    if m.profileRefreshTokenJob <> invalid
        return
    end if

    if m.scene = invalid
        m.scene = m.top.getScene()
    end if
    if m.profilesService = invalid
        m.profilesService = m.scene.findNode("ProfilesService")
    end if
    if m.jobQueue = invalid
        m.jobQueue = m.scene.findNode("JobQueue")
    end if

    LogInfo("Refreshing access token for profile:", m.top.username)

    input = {
        profile: m.top
        profilesService: m.profilesService
        accessTokenObject: {
            clientId: m.top.clientId
            clientSecret: m.top.clientSecret
            refreshToken: m.top.refreshToken
        }
    }

    m.profileRefreshTokenJob = JobSystem.CreateCallback(OnProfileRefreshTokenJobDone)
    JobSystem.QueueJob(m.jobQueue, Jobs.ProfileRefreshTokenJob, input, m.profileRefreshTokenJob)
end function

function OnProfileRefreshTokenJobDone(event as object) as void
    JobSystem.UnobserveCallback(event.getRoSGNode())
    m.profileRefreshTokenJob = invalid
end function
