import "pkg:/components/Services/Invidious/InvidiousService.bs"

@asynctask
function SubscribeTask(input as object) as object
    channelView = input.channelView
    invidiousNode = input.invidious
    ucid = input.ucid
    subscribe = input.subscribe

    if m.top.cancel
        return invalid
    end if

    service = new Invidious.InvidiousService(invidiousNode)

    if subscribe
        response = service.Subscribe(ucid, m.top.cancellation)
    else
        response = service.Unsubscribe(ucid, m.top.cancellation)
    end if

    if not response.IsSuccess()
        error = response.ErrorMessage()
        LogError("Failed to subscribe/unsubscribe to channel:", error)
        return invalid
    end if

    channelView.isSubscribed = subscribe
    return invalid
end function
