namespace CryptoUtils
    function GetSha256(input as string) as string
        return GetEvpDigest(input, "sha256")
    end function

    function GetSha1(input as string) as string
        return GetEvpDigest(input, "sha1")
    end function

    function GetMd5(input as string) as string
        return GetEvpDigest(input, "md5")
    end function

    function GetEvpDigest(input as string, method as string) as string
        buffer = CreateObject("roByteArray")
        buffer.FromAsciiString(input)

        digest = CreateObject("roEVPDigest")
        digest.Setup(method)
        return digest.Process(buffer)
    end function

    function ChannelEncrypt(input as string) as string
        return Encrypt(input, "channel")
    end function

    function ChannelDecrypt(input as string) as string
        return Decrypt(input, "channel")
    end function

    function Encrypt(input as string, encType as string) as string
        if input = ""
            return ""
        end if

        deviceCrypto = CreateObject("roDeviceCrypto")

        buffer = CreateObject("roByteArray")
        buffer.FromAsciiString(input)

        encrypted = deviceCrypto.Encrypt(buffer, encType)
        if encrypted = invalid
            return ""
        end if
        return encrypted.ToHexString()
    end function

    function Decrypt(input as string, encType as string) as string
        if input = ""
            return ""
        end if

        deviceCrypto = CreateObject("roDeviceCrypto")

        buffer = CreateObject("roByteArray")
        buffer.FromHexString(input)

        if buffer.Count() = 0
            return ""
        end if

        decrypted = deviceCrypto.Decrypt(buffer, encType)
        if decrypted = invalid
            return ""
        end if
        return decrypted.ToAsciiString()
    end function
end namespace
